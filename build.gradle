plugins {
    id 'java'
    id 'application'
    id 'org.springframework.boot' version '2.5.5'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'checkstyle'
    id 'jacoco'
    id 'org.liquibase.gradle' version '2.0.4'
    id 'org.siouan.frontend-jdk11' version '6.0.0'
    id "com.github.johnrengelman.processes" version "0.5.0"
    id "org.springdoc.openapi-gradle-plugin" version "1.6.0"
}

application {
    mainClass = "hexlet.code.AppApplication"
}

group = 'hexlet.code'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'


repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor(
            'org.mapstruct:mapstruct-processor:1.5.3.Final',
            'com.querydsl:querydsl-apt:5.0.0:jpa',
            'javax.annotation:javax.annotation-api:1.3.2',
            'org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.2.Final'
    )
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.mapstruct:mapstruct:1.5.3.Final'
    implementation 'org.liquibase:liquibase-core:4.6.1'
    implementation 'io.jsonwebtoken:jjwt:0.9.1'
    implementation 'com.github.javafaker:javafaker:1.0.2'
    implementation 'com.querydsl:querydsl-core:5.0.0'
    implementation 'com.querydsl:querydsl-jpa:5.0.0'
    implementation 'org.springdoc:springdoc-openapi-ui:1.6.15'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    liquibaseRuntime(
            sourceSets.main.output,
            'org.liquibase:liquibase-core:4.6.1',
            'org.liquibase.ext:liquibase-hibernate5:4.5.0',
            'org.springframework.boot:spring-boot-starter-data-jpa'
    )

    runtimeOnly(
            'com.h2database:h2:1.4.200',
            'org.postgresql:postgresql:42.2.24'
    )
}

task stage(dependsOn: ['clean', 'installDist'])
installDist.mustRunAfter clean

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

diffChangeLog {
    dependsOn compileJava
}

// Настраиваем Liquibase
liquibase {
    activities {
        main {
            // Указываем путь, по которому будет сгенерирован файл миграции
            changeLogFile 'src/main/resources/db/changelog/changelog-master.yaml'
            // Указывем источник, с которым будут сравниваться изменения
            // Это база данных, изначально она пустая
            url 'jdbc:h2:./blog'
            // Имя пользователя и пароль для подключения к базе
            username 'user'
            password 'password'
            // Сравниваем с моделями, задавая пакет
            referenceUrl 'hibernate:spring:hexlet.code.entity.' +
                    // Указываем диалект
                    '?dialect=org.hibernate.dialect.H2Dialect' +
                    // Указываем правила именования таблиц и столбцов,
                    // чтобы они соответствовали правилам Spring
                    '&hibernate.physical_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringPhysicalNamingStrategy'
        }
    }
}

frontend {
    nodeVersion = '16.13.1'
    installScript = 'install'
    assembleScript = 'run build'
    packageJsonDirectory = file("${projectDir}/frontend")
}

def configFrontendTasks = {
    inputs.files(fileTree("$projectDir/frontend").exclude('build', 'node_modules'))
    outputs.dir("$buildDir/resources/main/static")
}

assembleFrontend {
    configure configFrontendTasks

    doLast {
        copy {
            from "$projectDir/frontend/build"
            into "$buildDir/resources/main/static"
        }
    }
}

installFrontend {
    configure configFrontendTasks
}

processResources.dependsOn assembleFrontend